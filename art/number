#!/usr/bin/perl
use strict;
use warnings;
use Time::Local;
use Image::Magick;

my @ones = ( "", "One", "Two", "Three", "Four", 
	     "Five", "Six", "Seven", "Eight", "Nine" );
my @tens = ( "", "Teen", "Twenty", "Thirty", "Forty", 
	     "Fifty", "Sixty", "Seventy", "Eighty", "Ninety" );

my @gravities = ( "NorthWest", "NorthEast",
		  "West",      "East",
		  "SouthWest", "SouthEast" );

my $size      = "1500x1020";
my $bg        = "ivory";
my $red_init  =  0;
my $red_inc   =  2;
my $green_init=  25;
my $green_inc =  0;
my $blue_init =  255;
my $blue_inc  = -1;
my $gravity   =  $gravities[int(rand(@gravities))];
my $ps_init   =  130;
my $ps_inc    =  2.0;
my $slew_inc  =  4 + int(rand(5.0));
my $x_origin  =  500;
my $y_origin  =  400;
my $origin_inc=  rand(0.20);   # in radians
my $radius    =  int(rand(200));

my $text = "";

# calculate the number of days we've been living through this
my $then = timelocal(0,0,0,13,2,2020);   # March 13, 2020 --- lockdown
my $now = time();
my $days = int( ($now - $then) / (24 * 60 * 60) );
##printf "then:%d now:%d days since:%d\n", $then, $now, $days;

my $filename = $days ."-". $gravity ."-". $radius ."-.". 
    int(100 * $origin_inc) ."-". $slew_inc .".gif";

my $image = Image::Magick->new(size=>$size);
$image->ReadImage("canvas:".$bg);

for (my $i=1; $i<=$days; $i++) {

    makeText( $i );  ## Turn the integer into its English name, in global $text

    my $pointsize = $ps_init - ($i / $ps_inc);
    my $red       = $red_init + ($i * $red_inc);
    my $green     = $green_init + ($i * $green_inc);
    my $blue      = $blue_init + ($i * $blue_inc);
    my $fillcolor = "rgb(". $red .",". $green. ",". $blue .")";
    my $slew      = ($i * $slew_inc);
    my $x         = $x_origin + ( cos($i*$origin_inc) * $radius);
    my $y         = $y_origin + ( sin($i*$origin_inc) * $radius);
    my $geometry  = "+".$x."+".$y;

    $image->Annotate(
	text=>$text,
	pointsize=>$pointsize,
	fill=>$fillcolor, stroke=>"black",
	geometry=>$geometry,
	rotate=>$slew,
	gravity=>$gravity,
	);

}

$image->Write( $filename );
$image->Display;
        
sub makeText {

    my $num = $_[0];
    doOnes( $num % 10 );
    doTens( ($num / 10 ) % 10 );
    doHundreds( ($num / 100 ) % 10 );
}
        
sub doOnes {
    $text = $ones[$_[0]];
}

sub doTens {
    my $num = $_[0];
    if ( $num == 0 )          { ;; }
    elsif ( $num > 1 )        { $text = $tens[$num] . " " . $text;  }
    elsif ( $text eq "" )     { $text = "Ten"; }
    elsif ( $text eq "One")   { $text = "Eleven"; }
    elsif ( $text eq "Two")   { $text = "Twelve"; }
    elsif ( $text eq "Three") { $text = "Thirteen"; }
    elsif ( $text eq "Five")  { $text = "Fifteen"; }
    else                      { $text = $text . "teen"; }
}


sub doHundreds {
    my $num =$_[0];
    if ($num > 0) { $text = $ones[$_[0]] . " Hundred " . $text; }
}
